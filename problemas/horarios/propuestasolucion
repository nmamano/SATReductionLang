reduction
{
  for i,s in in.subjects {
    // specific number of session of the subject
    numsessions = "exactly {s.hours}";
    for d in 0..4
      numsessions = numsessions + " {i}atday{d}";
    insertsat(numsessions);
    // with no more than one session per day
    for d in 0..4 {
      daybound = "";
      for h in 0..11
	daybound = daybound + " {i}at{d*12+h}";
      insertsat("{i}atday{d} => exactly 1"+daybound);
      insertsat("{i}atday{d} <= atleast 1"+daybound);
    }
    // with one specific teacher
    oneteacher = "exactly 1";
    for t in s.teachers
      oneteacher = oneteacher + " {i}by{t}";
    insertsat(oneteacher);
    // with one specific classroom
    oneclassroom = "exactly 1";
    for c in s.classrooms
      oneclassroom = oneclassroom + " {i}in{c}";
    insertsat(oneclassroom);
  }
  // the teacher can attend all the sessions
  for i,s in in.subjects, t in s.teachers, h in in.teachers[t]
	  insertsat("{i}by{t} => -{i}at{h}");
  // sessions from the same course do not overlap, and overlaping
  // sessions (from different courses) have distinct teacher/classroom
  insertsat("true");
  for i , s0 in in.subjects,j , s1 in in.subjects 
      if (j>i)
	if (s0.course == s1.course){
	  nooverlap = "true";
	  for h in 0..59
	    nooverlap = nooverlap + " & -({i}at{h} & {j}at{h})";
	  insertsat(nooverlap);
	}else
	  for h in 0..59{
	    nooverlap = "{i}at{h} & {j}at{h} => true";
      for c in 0..in.classrooms-1
	      nooverlap = nooverlap + " & -({i}in{c} & {j}in{c})";
      for t in 0..in.teachers.size-1
	      nooverlap = nooverlap + " & -({i}by{t} & {j}by{t})";
	    insertsat(nooverlap);
	  }
  // at most 6 hours/day per course, and no spare hours between classes
  insertsat("-false");
  for c in 0..in.courses-1 {
    for h in 0..59 {
      coursehour = "course{c}at{h} <=> false";
      for i,s in in.subjects
	if (s.course == c)
	  coursehour = coursehour + " | {i}at{h}";
      insertsat(coursehour);
    }
    for d in 0..4 {
      daybound = "atmost 6";
      for h in 0..11
	daybound = daybound + " course{c}at{d*12+h}";
      insertsat(daybound);
      for h1 in 1..10 {
	compact = "course{c}at{d*12+h1-1} & -course{c}at{d*12+h1} => true";
  for h2 in h1+1..11
	  compact = compact + " & -course{c}at{d*12+h2}";
	insertsat(compact);
      }
    }
  }
}

reconstruction
{
  for i,s in in.subjects {
    for c in s.classrooms
      if (model["{i}in{c}"])
	res[i].classroom = c;
    for t in s.teachers
      if (model["{i}by{t}"])
	res[i].teacher = t;
    for(h=0 ; h<60 ; ++h)
      if (model["{i}at{h}"])
	res[i].hours.push = h;
  }
}
